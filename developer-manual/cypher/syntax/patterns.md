### 3.2.7. 패턴 (Patterns)

패턴 및 패턴 일치는 Cypher의 핵심이며 Cypher를 효과적으로 사용하려면 패턴을 잘 이해해야합니다.

패턴을 사용하면 찾고있는 데이터의 모양을 설명 할 수 있습니다. 예를 들어, `MATCH` 절에서 패턴을 사용하여 도형을 설명하고 Cypher는 해당 데이터를 가져 오는 방법을 파악합니다.


이 패턴은 일반적으로 화이트 보드에 속성 그래프 데이터의 모양을 그리는 방법과 매우 유사한 양식을 사용하여 데이터를 설명합니다. 일반적으로 관계를 나타 내기 위해 원 (노드를 나타내는)과 화살표 사이에 화살표가 그려져 있습니다.

패턴은 Cypher의 여러 위치 ( `MATCH`, `CREATE`, `MERGE` 절 및 패턴 표현식)에 나타납니다. 각각에 대한 자세한 내용은 다음에서 설명합니다.

[3.3.1, "MATCH"](../clauses/match.html)
[3.3.2, "OPTIONAL MATCH"](../clauses/optional-match.html)
[3.3.7, "CREATE"](../clauses/create.html)
[3.3.8, "MERGE"](../clauses/merge.html)
[3.3.3.4, "Using path patterns in WHERE"](../clauses/where.html#query-where-patterns)

#### 3.2.7.1. 노드패턴
패턴에서 설명 할 수있는 가장 단순한 '모양'은 노드입니다. 노드는 한 쌍의 괄호를 사용하여 설명되며 일반적으로 이름이 지정됩니다. 예 :

```
(a)
```
이 간단한 패턴은 단일 노드를 설명하고 변수 a 를 사용하여 해당 노드의 이름을 지정합니다. .

#### 3.2.7.2. 관련노드의 패턴

더 흥미로운 점은 여러 노드와 노드 간의 관계를 설명하는 패턴입니다. Cypher 패턴은 두 노드 사이에 화살표를 사용하여 관계를 설명합니다. 예 :

```
(a)-->(b)
```
이 패턴은 매우 단순한 데이터 모양을 묘사합니다 : 두 개의 노드와 하나의 관계. 이 예에서 두 노드는 각각 a 와 b 로 명명되며 관계는 방향성을 가집니다. 여기서는 a 에서 b 로 갑니다.

노드와 관계를 설명하는이 방법은 임의의 수의 노드와 노드 간의 관계를 포괄하도록 확장 될 수 있습니다. 예:
```
(a)-->(b)<--(c)
```

이러한 일련의 연결된 노드와 관계를 "경로(path)"라고합니다.

이 패턴에서 노드의 이름 지정은 나중에 패턴 또는 Cypher 쿼리의 다른 위치에서 동일한 노드를 다시 참조 해야하는 경우에만 필요합니다. 이것이 필요하지 않은 경우, 다음과 같이 이름을 생략 할 수 있습니다.

```
(a)-->()<--(c)
```

#### 3.2.7.3. 레이블

패턴의 노드 모양을 간단히 설명하는 것 외에도 속성을 설명 할 수 있습니다. 패턴에서 설명 할 수있는 가장 단순한 속성은 노드에 있어야하는 레이블입니다. 예 :

```
(a:User)-->(b)
```

여러 개의 레이블이있는 노드를 설명 할 수도 있습니다.

```
(a:User:Admin)-->(b)
```

#### 3.2.7.4. 속성 지정하기

노드와 관계는 그래프의 기본 구조입니다. Neo4j는 훨씬 더 풍부한 모델을 허용하기 위해이 두 가지 속성을 사용합니다.


Properties can be expressed in patterns using a map-construct: curly brackets surrounding a number of key-expression pairs, separated by commas. E.g. a node with two properties on it would look like:

```
(a { name: 'Andres', sport: 'Brazilian Ju-Jitsu'})
```

그것에 대한 기대와의 관계는 다음과 같이 보일 수 있습니다.:
```
(a)-[{blocked: false}]->(b)
```
속성이 패턴에 나타나면 데이터의 모양에 추가 제약 조건을 추가합니다. `CREATE` 절의 경우 등록 정보가 새로 작성된 노드와 관계에 설정됩니다. `MERGE` 절의 경우 속성은 기존 데이터에 있어야하는 모양에 대한 추가 제약 조건으로 사용됩니다 (지정된 속성은 그래프의 기존 데이터와 정확히 일치해야 함). 일치하는 데이터가 없으면 `MERGE` 는 `CREATE` 처럼 작동하고 새로 작성된 노드 및 관계에 등록 정보가 설정됩니다.

`CREATE` 제공되는 패턴은 단일 매개 변수를 사용하여 속성을 지정할 수 있습니다. 예 : `CREATE (node $paramName)` . Cypher가 쿼리를 컴파일 할 때 속성 이름을 알아야 일치가 효과적으로 수행 될 수 있으므로 다른 절에 사용 된 패턴에서는 이 작업을 수행 할 수 없습니다.

#### 3.2.7.5. 관계 설명

관계를 설명하는 가장 간단한 방법은 이전 예제와 같이 두 노드 사이의 화살표를 사용하는 것입니다. 이 기술을 사용하여 관계가 존재해야 함과 그것의 방향성을 설명 할 수 있습니다. 관계 방향에 신경 쓰지 않는다면 화살표 머리를 생략 할 수 있습니다.

```
(a)--(b)
```

노드와 마찬가지로 관계에도 이름이 부여 될 수 있습니다. 이 경우 대괄호 쌍을 사용하여 화살표를 분해하고 변수를 사이에 배치합니다. 예 :

```
(a)-[r]->(b)
```

노드의 레이블과 마찬가지로 관계에도 유형이있을 수 있습니다. 특정 유형과의 관계를 설명하려면 다음과 같이 지정할 수 있습니다.

```
(a)-[r:REL_TYPE]->(b)
```

레이블과 달리 관계에는 하나의 유형만 있을 수 있습니다. 그러나 관계에 일련의 유형 중 하나가 있을 수 있도록 일부 데이터를 설명하려면 파이프 기호로 구분하여 패턴에 모두 나열 할 수 있습니다. 다음과 같습니다 :

```
(a)-[r:TYPE1|TYPE2]->(b)
```

이 형식의 패턴은 기존 데이터를 설명하는데만 사용할 수 있습니다 (즉, `MATCH` 또는 표현식으로 패턴을 사용할 때). 여러 유형의 관계를 만들 수 없으므로 `CREATE` 또는 `MERGE`에서는 작동하지 않습니다.


노드와 마찬가지로 관계의 이름은 항상 생략 할 수 있습니다.이 경우에는 다음과 같습니다.

```
(a)-[:REL_TYPE]->(b)
```

가변 길이

<table width=1200><tr><td><i class="fa icon-alert-circle" title="important"></i><img src="./img/note.gif" width="70"></td><td width=90%>버전 2.1.x 및 이전 버전에서의 가변 길이 패턴 일치는 단일 MATCH 절 내부에 설명 된 패턴에 대한 관계 고유성을 적용하지 않습니다. 이것은 `MATCH (a)-[r]->(b), (a)-[rs*]->(c) RETURN *` 과 같은 쿼리가 `r` 을 `rs` 집합의 일부로 포함 할 수 있음을 의미합니다. 이 동작은 버전 2.2.0 이상에서 `r` 이 결과 집합에서 제외되는 방식으로 변경되었습니다. 여기서는 [3.1.4 절 "고유성"](../introduction/uniqueness.html) 에서 설명 하는 관계 고유성 규칙을 보다 잘 준수합니다. 관계 고유성 규칙이 일반적으로 지시하는대로 관계를 무시하지 않고 관계를 되돌려 야하는 쿼리 패턴이있는 경우 다음과 같이 여러 일치 절을 사용하여 이를 수행 할 수 있습니다. `MATCH (a)-[r]->(b) MATCH (a)-[rs*]->(c) RETURN *`. 이것은 `MATCH` 절을 지원하는 모든 버전의 Neo4j에서 작동합니다 (2.0.0 이상).</td></tr><table>

패턴에 많은 노드 및 관계 설명의 시퀀스를 사용하여 긴 경로를 설명하는 대신 패턴의 관계 설명에서 길이를 지정하여 많은 관계 (및 중간 노드)를 설명 할 수 있습니다. 예 :

```
(a)-[*2]->(b)
```

이것은 3 개의 노드와 2 개의 관계의 그래프를 모두 한 경로 (길이 2의 경로)로 설명합니다. 이것은 다음과 같습니다.

```
(a)-->()-->(b)
```

길이의 범위도 지정할 수 있습니다. 이러한 관계 패턴을 '가변 길이 관계'라고합니다. 예 :

```
(a)-[*3..5]->(b)
```

최소 길이는 3이고 최대 값은 5입니다. 4 개의 노드와 3 개의 관계, 5 개의 노드와 4 개의 관계 또는 6 개의 노드와 5 개의 관계 그래프가 모두 단일 경로에 함께 연결되어 있습니다.


두 경계는 생략 될 수 있습니다. 예를 들어 길이가 3 이상인 경로를 설명하려면 다음을 사용하십시오.

```
(a)-[*3..]->(b)
```

길이가 5 이하인 경로를 설명하려면 다음을 사용하십시오.

```
(a)-[*..5]->(b)
```

두 경계는 생략 될 수 있으므로 길이의 경로를 설명 할 수 있습니다.

```
(a)-[*]->(b)
```

간단한 예제로 아래 쿼리를 살펴 보겠습니다.

**질문**
```Javascript
MATCH (me)-[:KNOWS*1..2]-(remote_friend)
WHERE me.name = 'Filipa'
RETURN remote_friend.name
```

**결과**
```
+--------------------+
| remote_friend.name |
+--------------------+
| "Dilshad"          |
| "Anders"           |
+--------------------+
2 rows
```

이 쿼리는 그래프에서 패턴에 맞는 모양의 데이터를 찾습니다. 특히 노드 (이름이 'Filipa'인 속성)와 `KNOWS` 관련 노드가 하나 또는 두 단계 빠져 있습니다. 이것은 1 학년 및 2 학년 친구들을 찾는 전형적인 예입니다.


가변 길이 관계는 `CREATE` 및 `MERGE` 와 함께 사용할 수 없습니다.


#### 3.2.7.6. 경로 변수에 지정하기

위에서 설명한 것처럼 일련의 연결된 노드와 관계를 "경로"라고합니다. Cypher는 다음과 같이 식별자를 사용하여 경로의 이름을 지정할 수 있습니다.

```
p = (a)-[*3..5]->(b)
```

`MATCH`, `CREATE` 및 `MERGE` 에서 이 작업을 수행 할 수 있지만 패턴을 표현식으로 사용하는 경우에는 수행 할 수 없습니다.
